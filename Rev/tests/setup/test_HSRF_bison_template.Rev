## NOTE: This script is meant to be sourced within Rev from the Rev/tests folder


seed(THISSEED)

###------------------------------
##          Data 
###------------------------------

### Read in the sequence alignment
data <- readDiscreteCharacterData("full_model/data/THISALN.nex")

num_taxa <- data.ntaxa()
grid_end <- THISGRID  #this is edge of infinite grid cell

### Read in taxa
taxa = readTaxonData("full_model/data/THISALN.taxa.txt"," ")
num_taxa = taxa.size()

###-------------------------------
##          Set Grid    
###-------------------------------

# set my move index
mi = 0

# set move index for up-down moves
miud = 0

# define number of population sizes through time
NUM_INTERVALS = 99 + 1
NIM1 = NUM_INTERVALS - 1

# Chop up duration from root to present into equally sized intervals
interval_times <- grid_end * seq(1, NIM1, 1)/NIM1

######################################
# Define population sizes and priors #
######################################


zeta <- 0.005
gamma ~ dnHalfCauchy(0, 1)

gamma.setValue(runif(1, 0.05, 0.5)[1])

#print("got past sig model", "\n")


# Prior on population size at the present (diffuse)
zlog_Ne1 ~ dnNormal(0, 1)

moves[++mi] = mvSlideBactrian(zlog_Ne1,weight=1.7/100.0,sigma=1.0,tune=true) # RevBayes targets an acceptance of ~44% when autotuning by default, we can change this as we desire, as in this parameter where sampling has a lot of trouble

print("got to Ne model", "\n")

# Start accumulating vector of population sizes
logNe[1] := THISMU + 5*zlog_Ne1
Ne[1] := exp(zlog_Ne1)

# Flesh out the population sizes with skyride model
for (i in 1:(NUM_INTERVALS-1)) {
    index = i+1
    
    # make a HSRF local shrinkage variable
    sigma[i] ~ dnHalfCauchy(0,1)
    
    # specify normal priors on the difference between sequential population sizes
    # we use a non-centralized parameterization
    delta_logNe[i] ~ dnNormal( mean=0, sd=sigma[i]*gamma*zeta )
    
    # calculate population size in interval
    logNe[index] := logNe[i] + delta_logNe[i]
    Ne[index] := exp(logNe[index])

}


moves[++mi] = mvEllipticalSliceSamplingSimple(delta_logNe,weight=1.7/100.0,tune=false)
moves[++mi] = mvHSRFHyperpriorsGibbs(gamma, sigma, delta_logNe, zeta, weight=1.6/100.0)

############################
# Coalescent prior on tree #
############################

#print("got to tree model", "\n")

# Define our model
psi ~ dnHeterochronousCoalescentSkyline(theta   = Ne, times   = interval_times,  method  = "specified", taxa   = taxa)

## Tree proposals
# Moves on node times (excluding root height)
moves[++mi] = mvNodeTimeSlideUniform(psi, weight=20.0/100.0)
moves[++mi] = mvNodeTimeScale(psi, weight=20.0/100.0)
moves[++mi] = mvSubtreeScale(psi, weight=20.0/100.0)

# Moves on root height
moves[++mi] = mvRootTimeScaleBactrian(psi, weight=4.0/100.0)

# Moves on tree topology
total_tree_weight_BEAST = 28
moves[++mi] = mvNarrow(psi, weight=9.0/(9.0+9.0+168.0)*total_tree_weight_BEAST/100.0)
moves[++mi] = mvNNI(psi, weight=9.0/(9.0+9.0+168.0)*total_tree_weight_BEAST/100.0)
moves[++mi] = mvFNPR(psi, weight=168.0/(9.0+9.0+168.0)*total_tree_weight_BEAST/100.0)

TL := psi.treeLength()
RA := psi.rootAge()

######################
# Substitution model #
######################

#print("got to sub model", "\n")

# Wrap up kappa and pi into HKY model
Q := fnJC(4)

clock_rate <- THISCLOCK

#############
# The Model #
#############

seq ~ dnPhyloCTMC(tree=psi, Q=Q, branchRates=clock_rate, nSites=data.nchar())

seq.clamp(data)

### workspace model wrapper ###
mymodel = model(Ne)

### set up the monitors that will output parameter values to file and screen 
mni = 0
monitors[++mni] = mnModel(filename="full_model/output/HSRF_test_THISALN_THISREP.log",printgen=64000, separator = TAB)
monitors[++mni] = mnFile(filename="full_model/output/HSRF_test_THISALN_THISREP.trees",printgen=64000, separator = TAB, psi)
monitors[++mni] = mnScreen(printgen=64000,Ne[1],Ne[50],Ne[99],gamma)

################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves)

### pre-burnin to tune the proposals ###
mymcmc.burnin(generations=1000000,tuningInterval=1e4) #1 million, or 10% of the length of the chain we want
mymcmc.operatorSummary()

### run the MCMC ###
mymcmc.run(generations=160000000)  #160 million
mymcmc.operatorSummary()



## quit ##
q()
